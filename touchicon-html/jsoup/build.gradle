apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.ben-manes.versions'

def libraryName = 'touchicon-html-jsoup'

group pj.groupId
archivesBaseName = libraryName
version pj.versions.name

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode pj.versions.code
        versionName pj.versions.name
    }
}

dependencies {
    implementation project(':touchicon')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jsoup:jsoup:1.11.3'

    testImplementation 'junit:junit:4.12'
    testImplementation "com.google.truth:truth:0.43"
    testImplementation "io.mockk:mockk:1.9.1"
    testImplementation 'org.robolectric:robolectric:3.8'
}

dokka {
    outputFormat = 'gfm'
    outputDirectory = "../../docs/dokka/touchicon-html"
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = pj.groupId + '.' + libraryName
        licenses = ['MIT']
        websiteUrl = pj.siteUrl
        issueTrackerUrl = pj.githubUrl + '/blob/master/LICENSE'
        vcsUrl = pj.githubUrl + '.git'
        issueTrackerUrl = pj.githubUrl + '/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name = libraryName
            url = pj.siteUrl

            packaging = 'aar'
            groupId = pj.groupId
            artifactId = libraryName
            version = pj.versions.name

            licenses {
                license {
                    name = 'The MIT License'
                    url = 'https://opensource.org/licenses/MIT'
                    distribution = 'repo'
                }
            }
            scm {
                connection pj.githubUrl + '.git'
                url pj.githubUrl
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file:build/maven')
            pom.project {
                groupId = pj.groupId
                artifactId = libraryName
                version = pj.versions.name
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
