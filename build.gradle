buildscript {
    def versionMajor = 0
    def versionMinor = 8
    def versionPatch = 0
    ext {
        kotlin_version = "1.3.72"
        pj = [
                versions : [
                        name: "${versionMajor}.${versionMinor}.${versionPatch}",
                        code: versionMajor * 10000 + versionMinor * 100 + versionPatch
                ],
                groupId      : "net.mm2d",
                siteUrl      : "https://github.com/ohmae/touch-icon-extractor",
                githubUrl    : "https://github.com/ohmae/touch-icon-extractor",
                scmConnection: "scm:git:https://github.com/ohmae/touch-icon-extractor.git"
        ]
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"

        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.28.0"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.5"
}

task jacocoMerge(
        type: JacocoMerge,
        group: "verification"
) {
    gradle.afterProject { project, _ ->
        if (project.rootProject != project && project.plugins.hasPlugin('jacoco')) {
            executionData "${project.buildDir}/jacoco/test.exec"
        }
    }
}

task jacocoMergedReport(
        type: JacocoReport,
        dependsOn: [tasks.jacocoMerge],
        group: "verification"
) {
    getExecutionData().from = jacocoMerge.destinationFile

    gradle.afterProject { project, _ ->
        if (project.rootProject != project && project.plugins.hasPlugin('jacoco')) {
            getSourceDirectories().from += "${project.projectDir}/src/main/java"
            getClassDirectories().from += project.fileTree(dir: "${project.buildDir}/classes/kotlin/main")
        }
    }
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
